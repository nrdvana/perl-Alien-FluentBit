use alienfile;
use Path::Tiny 'path';

probe sub {
   if (-f '/lib/fluent-bit/libfluent-bit.so'
      && -x '/opt/fluent-bit/bin/fluent-bit'
      && `/opt/fluent-bit/bin/fluent-bit --version` =~ /v([0-9.]+)/
   ) {
      return 'system';
   }
};

sys {
   gather sub {
      my $build= shift;
      my $prefix= $build->runtime_prop->{prefix};
      my $bin_dir= $build->runtime_prop->{bin_dir}= '/opt/fluent-bit/bin';
      `"$bin_dir/fluent-bit" --version` =~ /v([0-9.]+)/ or die; # tested above
      $build->runtime_prop->{version}= $1;
      # Need to add our own fluent-bit.h to an installed include dir because the headers
      # installed by fluent-bit-headers package for Debian are broken.
      # See https://github.com/fluent/fluent-bit/issues/7165
      my $custom_inc_dir= path("${prefix}/include");
      $custom_inc_dir->mkpath;
      path(__FILE__)->parent->child('include/fluent-bit.h')->copy($custom_inc_dir);
      $build->runtime_prop->{cflags}= "-I$custom_inc_dir";
      $build->runtime_prop->{libs}= "-L/lib/fluent-bit -Wl,-rpath,/lib/fluent-bit -lfluent-bit";
   };
};

share {
   requires 'Alien::cmake3';
   requires 'Alien::flex';
   requires 'Alien::bison';
   start_url 'https://github.com/fluent/fluent-bit/archive/refs/tags/v2.1.0.tar.gz';
   plugin Download => ();
   plugin Extract => 'tar.gz';
   plugin 'Build::CMake';
   plugin 'Gather::IsolateDynamic';
   build [
      [ '%{cmake}',
         @{ meta->prop->{plugin_build_cmake}->{args} },
         # ... put extra cmake args here ...
         '%{.install.extract}'
      ],
      '%{make} -j 10',
      '%{make} install',
   ];
};

